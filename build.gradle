plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id "org.sonarqube" version "2.8"
    id 'pmd'
}

group 'com.github.autobump'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
}

allprojects {
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.16.0'
    }
    test {
        useJUnitPlatform()
    }
    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled true
            html.enabled true
        }
    }

    test.finalizedBy jacocoTestReport

    checkstyle {
        toolVersion "8.30"
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    pmd {
        consoleOutput = true
        ignoreFailures = false
        toolVersion = "6.22.0"
        ruleSets = []
        ruleSetFiles = rootProject.files("config/pmd/ruleset.xml")
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "autobump_autobump"
        property "sonar.organization", "autobump"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacoco {
    toolVersion "0.8.5"
}

task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects.test
}

test.finalizedBy(codeCoverageReport)

// build jar with submodules
configurations {
    childJars
}

dependencies {
    subprojects.each {
        childJars project(it.path)
    }
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect { zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
    manifest {
        attributes 'Main-Class': 'com.github.autobump.cli.model.Autobump'
    }
}
